plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

wrapper {
    gradleVersion = '6.5.1'
}

group 'com.chesapeaketechnology.unitconversion'
version '0.0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

ext {
    sonatypeUsername = findCredentialValue('Username')
    sonatypePassword = findCredentialValue('Password')
    ext."signing.password" = findCredentialValue('KeyPassword')
}

dependencies {
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.6.2'
}

test {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
}

// Configure publishing to the Sonatype Maven Central repo
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = 'Unit Conversion'
                description = 'Unit conversion library for Java'
                url = 'https://github.com/chesapeaketechnology/unit-conversion'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://github.com/chesapeaketechnology/unit-conversion/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'cti-service'
                        name = 'Chesapeake Technology International'
                        email = 'devops@ctic-inc.us'
                    }
                }
                scm {
                    url = 'https://github.com/chesapeaketechnology/unit-conversion'
                    connection = 'scm:git:git://github.com/chesapeaketechnology/unit-conversion.git'
                    developerConnection = 'scm:git:ssh://git@github.com:chesapeaketechnology/unit-conversion.git'
                }
                issueManagement {
                    system = 'GitHub'
                    url = 'https://github.com/chesapeaketechnology/unit-conversion/issues'
                }
            }
        }
    }

    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
        mavenLocal()
    }
}

// TODO
//signing {
//    sign publishing.publications
//}

/**
 * Try to locate a credential value (username or password) in the credentials store.
 *
 * @param value The suffix to use for the credential lookup key.  It will be appended to "sonatype".
 * @return The credential value if located.
 */
private String findCredentialValue(String value) {
    // TODO still need to address credentials for publishing to mavenCentral.
    return ""
//    return project.credentials."sonatype$value"
}